plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'pl.waskysoft.welcome'

    compileSdkVersion sdkVersion.compile
    buildToolsVersion buildToolsVer

    defaultConfig {
        minSdkVersion sdkVersion.min
        targetSdkVersion sdkVersion.target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    sourceSets {
        main.res.srcDirs += 'src/main/res-public'
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation 'androidx.preference:preference:1.2.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'pl.waskysoft'
            artifactId = 'welcome'
            version = "1.1.1"

            afterEvaluate {
                from components.release
            }
        }
    }
}